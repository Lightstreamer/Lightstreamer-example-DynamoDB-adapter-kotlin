plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.9.22'
    id 'maven-publish'
    id 'distribution'
}

wrapper {
    gradleVersion = '8.14'
}

repositories {
    mavenCentral()
}

ext {
    aws_version = '2.17.239'
    kotlinx_coroutines_version = '1.8.0'
}

group = 'com.lightstreamer.examples'
version = '1.0.1'

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/lightstreamer/Lightstreamer-example-DynamoDB-adapter-kotlin")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            artifact distZip
        }
    }
}

dependencies {
    api "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:$kotlinx_coroutines_version"
    api 'com.lightstreamer:ls-adapter-inprocess:7.3.1'
    api "software.amazon.awssdk:dynamodb:$aws_version"
}

distributions {
    main {
        distributionBaseName = "DynamoDB"

        contents {
            from(jar) {
                into "lib"
            }

            from (configurations.runtimeClasspath) {
                into "lib"
                exclude "ls-adapter-inprocess*"
                exclude "jsr305*"
            }
        }
    }
}

task fatJar(type: Jar, dependsOn: ['build']) {
    manifest {
        attributes "Implementation-Title": project.name, "Implementation-Version": project.version, 'Main-Class': 'demo.DemoPublisherKt'
    }
    archiveBaseName = 'ls-demo-dynamodb-fatjar'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "codegen-resources/*.*"
        exclude "META-INF/MANIFEST.MF"
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
        exclude "META-INF/*.RSA"
        exclude "META-INF/DEPENDENCIES*"
        exclude "META-INF/INDEX.LIST"
        exclude "META-INF/LICENSE*"
        exclude "META-INF/NOTICE*"
    }
    with jar
}
