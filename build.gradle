buildscript {
    ext.aws_version = '2.17.233'
    ext.kotlin_version = '1.7.10'
    ext.kotlinx_coroutines_version = '1.6.4'
    ext.kotlin_stdlib = "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

wrapper {
    gradleVersion = '7.5'
}

repositories {
    mavenCentral()
}

apply plugin: 'application'
apply plugin: 'kotlin'

mainClassName = 'demo.DemoPublisherKt'

dependencies {
    api kotlin_stdlib
    api "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:$kotlinx_coroutines_version"
    api 'com.lightstreamer:ls-adapter-inprocess:7.3.1'
    api "software.amazon.awssdk:dynamodb:$aws_version"
}

task(dist, description: 'Build LS dist folder', dependsOn: [':build']).doLast {
    String destDir = "build/dist"
    if (!file(destDir).deleteDir()) throw new Exception("Unable to delete $destDir")
    file("$destDir/classes/").mkdirs()
    file("$destDir/lib/").mkdirs()

    // copy adapter configuration
    copy {
        from "src/main/adapter"
        into destDir
    }

    // copy build output
    copy {
        from "build/classes/kotlin/main/"
        into "$destDir/classes/"
    }

    copy {
        from(configurations.runtimeClasspath.files)
        exclude "ls-inprocess-adapter-*.jar"
        into "$destDir/lib/"
    }
}

task fatJar(type: Jar, dependsOn: ['build']) {
    manifest {
        attributes "Implementation-Title": project.name, "Implementation-Version": project.version, 'Main-Class': 'demo.DemoPublisherKt'
    }
    archiveName = 'ls-dynamodb-demo-fatjar.jar'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "codegen-resources/*.*"
        exclude "META-INF/MANIFEST.MF"
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
        exclude "META-INF/*.RSA"
        exclude "META-INF/DEPENDENCIES*"
        exclude "META-INF/INDEX.LIST"
        exclude "META-INF/LICENSE*"
        exclude "META-INF/NOTICE*"
    }
    with jar
}
